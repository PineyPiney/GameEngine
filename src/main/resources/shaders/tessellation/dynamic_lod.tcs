#version 410 core

#define MIN_DIST 2
#define MAX_DIST 32

layout(vertices=4) out;

in vec2 texCoords[];

uniform mat4 model;
uniform mat4 view;

uniform int minTess;
uniform int maxTess;

out vec2 TextureCoord[];
out float ctrlColour[];

void main() {
	gl_out[gl_InvocationID].gl_Position = gl_in[gl_InvocationID].gl_Position;
	TextureCoord[gl_InvocationID] = texCoords[gl_InvocationID];

	if(gl_InvocationID == 0){

		// ----------------------------------------------------------------------
		// Step 2: transform each vertex into eye space
		vec4 viewSpacePos00 = view * model * gl_in[0].gl_Position;
		vec4 viewSpacePos10 = view * model * gl_in[1].gl_Position;
		vec4 viewSpacePos01 = view * model * gl_in[2].gl_Position;
		vec4 viewSpacePos11 = view * model * gl_in[3].gl_Position;

		// ----------------------------------------------------------------------
		// Step 3: "distance" from camera scaled between 0 and 1
		float distance00 = clamp((abs(viewSpacePos00.z) - MIN_DIST) / (MAX_DIST - MIN_DIST), 0.0, 1.0);
		float distance10 = clamp((abs(viewSpacePos10.z) - MIN_DIST) / (MAX_DIST - MIN_DIST), 0.0, 1.0);
		float distance01 = clamp((abs(viewSpacePos01.z) - MIN_DIST) / (MAX_DIST - MIN_DIST), 0.0, 1.0);
		float distance11 = clamp((abs(viewSpacePos11.z) - MIN_DIST) / (MAX_DIST - MIN_DIST), 0.0, 1.0);

		// ----------------------------------------------------------------------
		// Step 4: interpolate edge tessellation level based on closer vertex
		float tessLevel0 = mix( maxTess, minTess, min(distance01, distance00) );
		float tessLevel1 = mix( maxTess, minTess, min(distance00, distance10) );
		float tessLevel2 = mix( maxTess, minTess, min(distance10, distance11) );
		float tessLevel3 = mix( maxTess, minTess, min(distance11, distance01) );

		// ----------------------------------------------------------------------
		// Step 5: set the corresponding outer edge tessellation levels
		gl_TessLevelOuter[0] = tessLevel0;
		gl_TessLevelOuter[1] = tessLevel1;
		gl_TessLevelOuter[2] = tessLevel2;
		gl_TessLevelOuter[3] = tessLevel3;

		ctrlColour[0] = min(distance01, distance00);

		// ----------------------------------------------------------------------
		// Step 6: set the inner tessellation levels to the max of the two parallel edges
		gl_TessLevelInner[0] = max(tessLevel1, tessLevel3);
		gl_TessLevelInner[1] = max(tessLevel0, tessLevel2);
	}
}
