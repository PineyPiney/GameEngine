#version 410 core
#define Z_FACTOR 0.006292406963139062

layout(quads, fractional_even_spacing, ccw) in;

in vec2 TextureCoord[];
in float ctrlColour[];

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

uniform float scale;
uniform sampler2D heightMap;

out vec2 texCoords;
out vec4 colour;

void main(){

	float u = gl_TessCoord.x;
	float v = gl_TessCoord.y;

	vec2 t0 = TextureCoord[0];
	vec2 t1 = TextureCoord[1];
	vec2 t2 = TextureCoord[2];
	vec2 t3 = TextureCoord[3];

	vec2 t4 = (t1 - t0) * u + t0;
	vec2 t5 = (t3 - t2) * u + t2;
	texCoords = (t5 - t4) * v + t4;

	vec4 p0 = gl_in[0].gl_Position;
	vec4 p1 = gl_in[1].gl_Position;
	vec4 p2 = gl_in[2].gl_Position;
	vec4 p3 = gl_in[3].gl_Position;

	vec4 p4 = (p1 - p0) * u + p0;
	vec4 p5 = (p3 - p2) * u + p2;
	vec4 p = (p5 - p4) * v + p4;

	vec4 normal = vec4(normalize(cross((p1 - p0).xyz, (p2 - p0).xyz)), 0);
	float height = texture(heightMap, texCoords).r;

	float heightScale = Z_FACTOR * textureSize(heightMap, 0).x * scale;
	gl_Position = projection * view * model * (p + normal * height * heightScale);

	float c = ctrlColour[0];
	colour = vec4(c, c, c, 1.0);
}